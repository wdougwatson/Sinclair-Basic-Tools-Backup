 100 PAPER 0: INK 0: BORDER 7: INVERSE 0: OVER 0: FLASH 0: CLS
 110 DATA 0,126,126,126,126,126,126,0
 120 DATA 0,60,126,126,126,126,60,0
 130 FOR a=USR "a" TO USR "a"+15: READ b: POKE a,b: NEXT a
 140 RANDOMIZE : LET t=10: LET s=0: LET c=10
 150 PRINT INK 6;"SCORE : 0"
 160 FOR a=1 TO 10: PRINT INK 6;AT 0,21+a;CHR$ 145: NEXT a
 170 FOR a=1 TO 5: FOR b=1 TO 21
 180 PRINT PAPER 7; INK 6-a;AT b,a;CHR$ 144
 190 NEXT b: NEXT a
 200 FOR t=1 TO 10: PRINT AT 0,21+t;"*"
 300 LET x=6: LET y=1+INT (21*RND )
 310 LET h=1: LET v=1: IF RND >.5 THEN LET v=-1
 400 PRINT INK 6;AT y,x;CHR$ 145
 410 IF INKEY$ ="k" AND c>1 THEN PRINT AT c,31;"*": LET c=c-1
 420 IF INKEY$ ="m" AND c<21 THEN PRINT AT c,31;"*": LET c=c+1
 430 PRINT INK 7;AT c,31;CHR$ 138: IF x>30 THEN GO TO 600
 440 IF x=30 THEN LET d=c-y: IF d=0 OR d=v THEN LET h=-1: BEEP .02,12: IF d=v THEN LET v=-v
 450 IF x<1 THEN LET h=1: BEEP .02,0
 460 IF y<2 THEN LET v=1: BEEP .02,0
 470 IF y>20 THEN LET v=-1: BEEP .02,0
 480 PRINT AT y,x;"*": LET x=x+h: LET y=y+v: LET a=ATTR (y,x)
 490 LET a=a-8*INT (a/8): IF a=0 OR a=7 THEN GO TO 400
 500 PRINT INK 7;AT y,x;"*": BEEP .05,12+3*a
 510 LET s=s+a: PRINT INK 6;AT 0,8;s; INK 0;AT y,x;"*"
 520 GO TO 300
 600 BEEP .1,-20: PRINT AT y,x;"*"
 610 NEXT t
 700 PRINT INK 6;AT 0,21;"GAME OVER"
